rootProject.name = 'ktor-samples'

enableFeaturePreview('GRADLE_METADATA')

def module(group, name) {
    include(name)
    project(":$name").projectDir = file("$group/$name")
}

// ---------------------------

module('app', 'chat')
module('app', 'httpbin')
module('app', 'kweet')
module('app', 'youkube')

module('deployment', 'netty')
module('deployment', 'netty-embedded')
module('deployment', 'jetty')
module('deployment', 'jetty-embedded')
module('deployment', 'jetty-war')
module('deployment', 'tomcat-war')
module('deployment', 'google-appengine-standard')
module('deployment', 'docker')

module('feature', 'async')
module('feature', 'auth')
module('feature', 'custom-feature')
module('feature', 'guice')
module('feature', 'gson')
module('feature', 'http2-push')
module('feature', 'html-widget')
module('feature', 'jackson')
module('feature', 'json-client')
module('feature', 'locations')
module('feature', 'metrics')
module('feature', 'post')
module('feature', 'sessions')
module('feature', 'ssl')
module('feature', 'static-content')
module('feature', 'testable')

module('other', 'css-dsl')
module('other', 'maven-netty')
module('other', 'maven-google-appengine-standard')
module('other', 'proguard')
module('other', 'multiple-connectors')
module('other', 'sandbox')
module('other', 'simulate-slow-server')
module('other', 'rx')
module('other', 'filelisting')
module('other', 'structured-logging')
module('other', 'client-multipart')
module('other', 'client-tools')
module('other', 'di-kodein')

include('client-mpp')
project(':client-mpp').projectDir = file("mpp/client-mpp")

def props = new Properties()
def f = file("local.properties")
if (f.exists()) {
    f.withInputStream { props.load(it) }
}

if (System.env['ANDROID_HOME'] != null || props["sdk.dir"] != null) {
    include('client-mpp:android')
    project(':client-mpp:android').projectDir = file('mpp/client-mpp/android')
} else {
    logger.warn("WARNING: Android not configured, please create a local.properties file including a sdk.dir property with your Android sdk path")
}

include('client-mpp:ios')
project(':client-mpp:ios').projectDir = file('mpp/client-mpp/ios')

include("fullstack-mpp-common")
project(":fullstack-mpp-common").projectDir = file("other/fullstack-mpp/common")

include("fullstack-mpp-frontend")
project(":fullstack-mpp-frontend").projectDir = file("other/fullstack-mpp/frontend")

include("fullstack-mpp-backend")
project(":fullstack-mpp-backend").projectDir = file("other/fullstack-mpp/backend")
