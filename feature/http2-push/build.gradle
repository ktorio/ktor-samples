buildscript {
    ext.tcnative_version = '2.0.27.Final'

    // Determine native library that we need for Netty SSL / HTTP2 support

    def osName = System.getProperty('os.name').toLowerCase()

    if (osName.contains('win')) {
        ext.tcnative_classifier = 'windows-x86_64'
    } else if (osName.contains('linux')) {
        ext.tcnative_classifier = 'linux-x86_64'
    } else if (osName.contains('mac')) {
        ext.tcnative_classifier = 'osx-x86_64'
    } else {
        ext.tcnative_classifier = ''
    }

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "io.ktor.samples.http2push.MainKt"

sourceSets {
    main.kotlin.srcDirs = [ 'src' ]
    main.resources.srcDirs = [ 'resources' ]
}

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"
    compile "io.ktor:ktor-network-tls:$ktor_version"
    compile "io.ktor:ktor-network-tls-certificates:$ktor_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    compile "io.netty:netty-tcnative:$tcnative_version"
    compile "io.netty:netty-tcnative-boringssl-static:$tcnative_version"
    compile "io.netty:netty-tcnative-boringssl-static:$tcnative_version:$tcnative_classifier"
}

