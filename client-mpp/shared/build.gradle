plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'com.android.library'
}

kotlin {
    android()
    ios {
        binaries {
            framework {
                baseName = 'shared'
            }
        }
    }
    macosX64()
    js(IR) {
        browser()
    }

    sourceSets.each {
        it.dependencies {
            implementation(project.dependencies.enforcedPlatform("io.ktor:ktor-bom:2.3.5"))
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "io.ktor:ktor-client-core"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation 'com.google.android.material:material:1.6.4'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
                implementation "io.ktor:ktor-client-android"
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation 'junit:junit:4.13.2'
            }
        }
        iosMain {
            dependencies {
                implementation "io.ktor:ktor-client-ios"
            }
        }
        iosTest {

        }
        jsMain {
            dependencies {
                implementation "io.ktor:ktor-client-js"
            }
        }
        macosX64Main {
            dependencies {
                implementation "io.ktor:ktor-client-core"
                implementation "io.ktor:ktor-client-curl"
            }
        }
    }
}

android {
    compileSdkVersion 30
    sourceSets["main"].manifest.srcFile("src/androidMain/AndroidManifest.xml")
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }
}

task(packForXcode, type: Sync) {
    group = 'build'
    def mode = System.getenv('CONFIGURATION') ?: 'DEBUG'
    def sdkName = System.getenv('SDK_NAME') ?: 'iphonesimulator'
    def targetName = 'ios' + (sdkName.startsWith('iphoneos') ? 'Arm64' : 'X64')
    def framework = kotlin.targets[targetName].binaries.getFramework(mode)
    inputs.property('mode', mode)
    dependsOn(framework.linkTask)
    def targetDir = new File(buildDir, 'xcode-frameworks')
    from({ framework.outputDirectory })
    into(targetDir)
}

tasks.getByName('build').dependsOn(packForXcode)
