buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.10"
    }
}

apply plugin: 'kotlin-multiplatform'

kotlin {
    targets {
        js('frontend', IR) {
            browser {
                testTask {
                    // TODO: disable browser tests since we can't run it on teamcity agents yet
                    enabled = false
                }
                binaries.executable()
            }
        }
        jvm('backend') {
        }
    }

    sourceSets.each {
        it.dependencies {
            implementation(project.dependencies.enforcedPlatform("io.ktor:ktor-bom:2.3.5"))
        }
    }

    sourceSets {
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
                implementation "org.jetbrains.kotlin:kotlin-test-common"
            }
        }
        backendMain {
            dependencies {
                implementation "io.ktor:ktor-server-netty"
                implementation "io.ktor:ktor-server-html-builder"
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
                implementation "ch.qos.logback:logback-classic:1.4.6"
            }
        }
        backendTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test"
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
            }
        }
        frontendMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
                implementation "org.jetbrains.kotlin:kotlin-test-js"
                implementation "io.ktor:ktor-client-core"
                implementation "io.ktor:ktor-client-js"
            }
        }
    }
}

repositories {
    mavenCentral()
}

tasks.getByName('frontendBrowserProductionWebpack') {
    outputFileName = 'output.js'
}

tasks.getByName('backendJar') {
    dependsOn(tasks.getByName('frontendBrowserProductionWebpack'))
    def frontendBrowserProductionWebpack = tasks.getByName('frontendBrowserProductionWebpack')
    from(new File(frontendBrowserProductionWebpack.destinationDirectory, frontendBrowserProductionWebpack.outputFileName))
}

task run(type: JavaExec, dependsOn: [backendJar]) {
    main = "io.ktor.samples.fullstack.backend.BackendCodeKt"
    classpath(configurations.backendRuntimeClasspath, backendJar)
    args = []
}
